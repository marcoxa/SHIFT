% -*- Mode: LaTeX -*-

% examples.tex --


% Copyright (c)1996, 1997, 1998 The Regents of the University of
% California (Regents). All Rights Reserved. 
%
% Permission to use, copy, modify, and distribute this software and its
% documentation for educational, research, and not-for-profit purposes,
% without fee and without a signed licensing agreement, is hereby
% granted, provided that the above copyright notice, this paragraph and
% the following two paragraphs appear in all copies, modifications, and
% distributions. 
%
% Contact The Office of Technology Licensing, UC Berkeley, 2150 Shattuck
% Avenue, Suite 510, Berkeley, CA 94720-1620, (510) 643-7201, for
% commercial licensing opportunities. 
%
% IN NO EVENT SHALL REGENTS BE LIABLE TO ANY PARTY FOR DIRECT, INDIRECT,
% SPECIAL, INCIDENTAL, OR CONSEQUENTIAL DAMAGES, INCLUDING LOST PROFITS,
% ARISING OUT OF THE USE OF THIS SOFTWARE AND ITS DOCUMENTATION, EVEN IF
% REGENTS HAS BEEN ADVISED OF THE POSSIBILITY OF SUCH DAMAGE. 
%
% REGENTS SPECIFICALLY DISCLAIMS ANY WARRANTIES, INCLUDING, BUT NOT
% LIMITED TO, THE IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR
% A PARTICULAR PURPOSE.  THE SOFTWARE AND ACCOMPANYING DOCUMENTATION, IF
% ANY, PROVIDED HEREUNDER IS PROVIDED "AS IS". REGENTS HAS NO OBLIGATION
% TO PROVIDE MAINTENANCE, SUPPORT, UPDATES, ENHANCEMENTS, OR
% MODIFICATIONS. 


\section{Example}

This is an incomplete and probably incorrect specification for a
single-lane, multi-car platoon scenario.  Cars have squirrel sensors,
and will brake for squirrels.  Cars are created with
different initial speeds (we omit the details of creation) so that
platoons will actually form.  Once they are formed, they never go away.

\begin{vcode}
type Car {
    state
	number v;
	SquirrelSensor sensor;
	Car car_ahead, car_behind;

    output
	number x, k, kbh, ka;

    export emergency, emergencyb, join_ahead, join_behind;

    flow
    	default { x' = v };

    discrete
	cruising		{ x' = 20 },
	platoon_head            { x' = 25 },
	in_platoon              { v' = - k * (x(car_ahead) - x - 3) },
	braking                 { v' = - kbh },
	stopped                 { v' = 0, x' = 0 },
	accelerating            { v' = ka };

    transition
	cruising -> platoon_head { join_behind,
				   car_behind:join_ahead },
	{ cruising, platoon_head } -> in_platoon
			{ join_ahead, car_ahead:join_behind },
	all - {stopped} -> braking { emergency,
				   sensor:emergency,
				   car_behind:emergencyb },
	all - {stopped} ->  braking { emergencyb,
				    car_behind:emergencyb },
	braking -> stopped {} when v = 0,
	{ stopped, braking } -> accelerating
				{ sensor:cancel_emergency },
	accelerating -> cruising {} when v = 20;
}

// A SquirrelSensor knows nothing about cars.
// As far as it is concerned, it could be mounted on a pole.
// I don't know how this sensor works.  Perhaps it could
// imagine squirrels at random intervals.

type SquirrelSensor {
    export emergency, cancel_emergency;

    discrete no_squirrel, squirrel;

    transition
	no_squirrel -> squirrel { emergency },
	squirrel -> no_squirrel { cancel_emergency };
}	

// This counts how many times cars had to brake for squirrels.
// Cars know nothing about this beacon, except they need to
// export their emergency event.
type Beacon {
    state
	number count;
	set(Car) observed;

    discrete idle;

    transition
	idle -> idle { observed:emergency (one) }
		do {count := count + 1; };
}
\end{vcode}
\end{document}
